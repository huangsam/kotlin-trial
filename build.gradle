plugins {
    id('org.jetbrains.kotlin.jvm') version('1.9.23')
    id('io.gitlab.arturbosch.detekt') version('1.23.5')
}

group = 'com.huangsam'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(21)
}

/**
 * Write build reports after running Detekt.
 * https://detekt.dev/docs/gettingstarted/gradle/
 */
tasks.named("detekt").configure {
    reports {
        xml.required.set(true)
        html.required.set(true)
        txt.required.set(true)
        sarif.required.set(true)
        md.required.set(true)
    }
}

/**
 * Enable auto-correction for Kotlin code via Detekt.
 * https://stackoverflow.com/q/57336354
 */
detekt {
    autoCorrect = true
}

/**
 * Get Detekt to run on each commit.
 * https://detekt.dev/docs/gettingstarted/git-pre-commit-hook/
 * https://www.delasign.com/blog/android-studio-gradle-precommit-git-script/
 */
tasks.register('addDetektHook') {
    exec {
        commandLine("cp", ".hooks/pre-commit", ".git/hooks")
    }
}
